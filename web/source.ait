hsl: "hsl(%s, %s%, %s%)" replace replace replace ;
rgb: "rgb(%s, %s, %s)" replace replace replace ;

2carthX:
  dup 1> [ 0> ] dip cos * ;

2carthY:
  dup 1> [ 0> ] dip sin * ;

2carth: ( 0 0 ) [ dup 2carthX ] dip swap <0 [ 2carthY ] dip swap <1 ;

2polarR:
  dup 0> dup * [ 1> dup * ] dip sqrt ;

2polarPhi:
  dup 1> [ 0> ] dip atan2 ;

2polar: ( 0 0 ) [ dup 2polarR ] dip swap <0 [ 2polarPhi ] dip swap <1 ;

2rad: 180 / PI * ;

2deg: PI / 180 * ;

<+0: [ dup 0> ] dip + <0 ;
<+1: [ dup 1> ] dip + <1 ;
<-0: [ dup 0> ] dip - <0 ;
<-1: [ dup 1> ] dip - <1 ;

random1L: dup 1 - + random * ;
random1U: random * ;
random2: dup [ - random * ] dip + ;

hsl: "hsl(%s, %s%, %s%)" replace replace replace ;
teal: 60 85 180 hsl ;
black: 0 0 0 hsl ;

topLeftCorner: <width -1 * 2 / <height  -1 * 2 / ;

fillBackground: topLeftCorner <width <height fillRect ;

random25: -25 25 random2 ;

calcNextControlPoint:
  random25 <+0
  3 2rad <+1 ;

calcNextAlongCircle:
  6 2rad <+1 ;

circle:
  dup [ 2carth moveTo ] dip
  [ dup [ calcNextControlPoint ] dip calcNextAlongCircle dup [ 2carth [ 2carth ] dip quadraticCurveTo ] dip ]
  [ 1> 2 PI * 6 2rad - <= ]
  while
  drop
  closePath ;

setTealStroke: teal strokeStyle ;
setPinkStroke: "#d36fc8" strokeStyle ;
setBlueStroke: "#2347e6" strokeStyle ;

config:
  <width 2 / <height 2 / translate
  2.5 lineWidth
  0.5 globalAlpha
  teal strokeStyle
  black fillStyle ;

config
[ fillBackground
  ( 175 0 ) circle setTealStroke stroke unstack
  ( 125 0 ) circle setPinkStroke stroke unstack
  ( 150 0 ) circle setBlueStroke stroke unstack ]
raf
